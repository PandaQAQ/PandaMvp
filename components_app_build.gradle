boolean isShellApp = project.getName() == shellApp
boolean isLaunchApp = project.getName() == launchApp
if (isLaunchApp) { // 壳 APP 始终以 application 模式运行，其他业务组件以依赖库模式根据配置拔插
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner rootProject.ext.dependencies["androidJUnitRunner"]
        multiDexEnabled true
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "boolean", "USE_CANARY", "true"
            minifyEnabled false
            if (isLaunchApp) {
                consumerProguardFiles 'proguard-rules.pro'
            } else {
                proguardFiles 'proguard-rules.pro'
            }
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "boolean", "USE_CANARY", "false"
            minifyEnabled true
            zipAlignEnabled true
            if (isLaunchApp) {
                consumerProguardFiles 'proguard-rules.pro'
            } else {
                proguardFiles 'proguard-rules.pro'
            }
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        disable "ResourceType"
        abortOnError false
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (isShellApp) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                if (isLaunchApp) {
                    manifest.srcFile 'src/main/manifest/component/AndroidManifest.xml'
                } else {
                    manifest.srcFile 'src/main/manifest/release/AndroidManifest.xml'
                }
            }
        }
    }
}
dependencies {
    api (rootProject.ext.dependencies["arouter"]){
        exclude group: 'com.android.support'
    }
    kapt rootProject.ext.dependencies["arouter_compiler"]
    implementation rootProject.ext.dependencies["annotation"]
    annotationProcessor rootProject.ext.dependencies["annotation"]
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api project(':uires')
    api(project(':router'))
}