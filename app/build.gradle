apply from: "../components_app_build.gradle"
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

boolean isLaunchApp = project.getName() == launchApp

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        if (isLaunchApp) {
            applicationId "com.pandaq.pandamvp"
            compileSdkVersion rootProject.ext.android["compileSdkVersion"]
            minSdkVersion rootProject.ext.android["minSdkVersion"]
            targetSdkVersion rootProject.ext.android["targetSdkVersion"]
            versionCode rootProject.ext.android["versionCode"]
            versionName rootProject.ext.android["versionName"]
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        multiDexKeepProguard file('multidex-config.pro')
        ndk {
            abiFilters "armeabi"
            abiFilters "arm64-v8a"
            abiFilters "armeabi-v7a"
            abiFilters "x86"
            abiFilters "x86_64"
        }
    }
    buildTypes {

        release {
            if (isLaunchApp) {
                minifyEnabled true
                shrinkResources true
                zipAlignEnabled true
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }

        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            minifyEnabled false
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //noinspection GradleCompatible
    implementation rootProject.ext.dependencies["appcompat-v7"]
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["test-runner"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]
    implementation rootProject.ext.dependencies["design"]
    //Picasso
    implementation rootProject.ext.dependencies["picasso"]
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    api(project(':uires')) {
        exclude group: 'com.android.support'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
    }
    implementation(project(':functionlibs:pay')) {
        exclude group: 'com.android.support'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
    }

    // 按需加载依赖业务 APP
    if (buildAll.toBoolean()) {
        for (String name : allComponents.split(",")) {
            implementation(project(":$name")) {
                exclude group: 'com.android.support'
                exclude module: 'appcompat-v7'
                exclude module: 'support-v4'
            }
        }
    } else {
        for (String name : loadComponents.split(",")) {
            if (!name.isEmpty()) {
                implementation(project(":$name")) {
                    exclude group: 'com.android.support'
                    exclude module: 'appcompat-v7'
                    exclude module: 'support-v4'
                }
            }
        }
    }
}
